# -*- mode: shell-script -*-
# zshenv
# This file is always executed by zsh so it's mostly useful to set environment
# variables used by non-interactive jobs.

# Helpers {{{
source_if_exists() {
    if test -e "$1"; then source "$1"; fi
}

add_path_if_exists() {
    if test -d "$1"; then path+=("$1"); fi
}
# }}}

# PATH handling {{{
# PATH and path refers to the same variable (tied), so as MANPATH and manpath.
# The attribute flag -U is to keep unique values inside an array.
typeset -U path manpath fpath cdpath

# set default PATH, keeping any existing system-wide PATH value.
path=(
    $path /sbin /bin /usr/sbin /usr/bin /usr/X11R6/bin 
    /usr/local/sbin /usr/local/bin
)

if [[ $OSTYPE = darwin* ]]; then
    # Homebrew
    if [[ -x /opt/homebrew/bin/brew ]]; then
        eval "$(/opt/homebrew/bin/brew shellenv)"
    fi

    # old path
    if [[ -x /usr/local/Homebrew/bin/brew ]]; then
        eval "$(/usr/local/Homebrew/bin/brew shellenv)"
    fi
fi

# Go support
if [[ -d $HOME/dev/go ]]; then
    export GOPATH=$HOME/dev/go
    path+=($HOME/dev/go/bin)
fi
add_path_if_exists "/usr/local/opt/go/libexec/bin"
add_path_if_exists "/usr/local/go/bin"

# Rust support
add_path_if_exists "$HOME/.cargo/bin"
source_if_exists "$HOME/.cargo/env"

# Other miscellaneous bin directories.
# If you have a ~/bin directory, add it to PATH (here I use the path parameter
# instead of the PATH environment variable).
if [[ -d $HOME/bin ]]; then
    path+=$HOME/bin
elif [[ -d $HOME/Preferences/bin ]]; then
    path+=$HOME/Preferences/bin
fi
# same for ~/local
add_path_if_exists "$HOME/local/bin"
add_path_if_exists "/opt/bin"
add_path_if_exists "/opt/local/bin"
add_path_if_exists "/usr/games"

# Poetry
add_path_if_exists "$HOME/.poetry/bin"
# }}}

# ripgrep
export RIPGREP_CONFIG_PATH=$HOME/.ripgreprc

# Format string for 'time' command:
TIMEFMT="%*E real time :: CPU: %P (%U user, %S kernel) :: %J"

# Set the umask
if [[ $UID == 0 ]]; then
    umask 077
else
    umask u=rwx,g=rx,o=
fi

# 11/06/2016
# Not having an umask of "022" breaks tools written by the cool kids (like kitchen)
umask 022

# stop the insanity
if [[ -d "$HOME/dev" ]]; then
    PERSONAL_CODE_DIR="$HOME/dev"
    OTHERS_CODE_DIR="$HOME/dev/others"
else
    PERSONAL_CODE_DIR="$HOME/code"
    OTHERS_CODE_DIR="$HOME/code/others"
fi
export PERSONAL_CODE_DIR OTHERS_CODE_DIR

# on macOS /etz/profile will call /usr/libexec/path_helper which will fuck PATH ignoring
# everything done so far.
# Also if we ever need to have this feature we can just read files in /etc/paths.d and /etc/manpaths.d by
# ourselves; see man 'path_helper'.
setopt no_GLOBAL_RCS

# Manually add paths from /etc/paths.d on macOS rather than calling path_helper; this is used for things like Go
# installed in /opt/go.
if [[ -d /etc/paths.d ]]; then
    for f in /etc/paths.d/*; do
	path+="$(cat $f)"
    done
fi

# Load local zshenv
source_if_exists "$HOME/.zshenv.local"

# vim: ft=zsh
